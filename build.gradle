plugins {
    id 'java'
    id 'maven'

    id 'idea'
    id 'eclipse'

    id 'net.minecrell.licenser' version '0.3'
    id 'org.spongepowered.plugin' version '0.8.1'
}

defaultTasks 'clean', 'licenseFormat', 'build'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.thevoxelbox'
archivesBaseName = project.name.toLowerCase()
version = '3.0.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

// Common dependencies
dependencies {
    compile 'org.spongepowered:spongeapi:7.0.0-SNAPSHOT'

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

// License header formatting
license {
    header file('LICENSE')
    include '**/*.java'
    newLine = false
}

// Source compiler configuration
configure([compileJava, compileTestJava]) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path']
    options.deprecation = true
    options.encoding = 'UTF-8'
}

processResources {
    // Include LICENSE in final JAR
    from 'LICENSE'
}

// Set manifest entries
jar {
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Created-By': "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})"
        )
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.links(
            'https://docs.oracle.com/javase/8/docs/api/'
    )
    // Disable the crazy super-strict doclint tool in Java 8
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}
